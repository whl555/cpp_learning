# CMU15445 中 CMake 的使用

## 项目代码组织结构

命令`tree  > show_tree.txt --gitignore -L 2`内容省略版；
可以看出包括主工程根目录下 CMakeLists.txt，以及 src、test、third_party、tools 四个子目录下的 CMakeLists.txt

```bash
.
├── CMakeLists.txt
├── src
│   ├── include
│   ├── primer
│   └── CMakeLists.txt
├── test
│   ├── include
│   ├── primer
│   └── CMakeLists.txt
├── third_party
│   ├── fmt
│   ├── googletest
│   └── CMakeLists.txt
└── tools
    ├── btree_bench
    └── CMakeLists.txt

56 directories, 23 files
```

## project - CMakeLists.txt

#### 1. 配置变量

```cmake
set(CMAKE_EXPORT_COMPILE_COMMANDS ON) # For clang-tidy
set(BUILD_SHARED_LIBS OFF) # We expect external libraries to be linked statically.
set(CMAKE_CXX_STANDARD 17) # Compile as C++17.
set(CMAKE_CXX_STANDARD_REQUIRED ON) # Require C++17 support.
```

#### 2. 引用头文件

```cmake
# Includes.
set(BUSTUB_SRC_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/src/include)
set(BUSTUB_TEST_INCLUDE_DIR ${PROJECT_SOURCE_DIR}/test/include)
set(BUSTUB_THIRD_PARTY_INCLUDE_DIR
        ${PROJECT_SOURCE_DIR}/third_party
        ${PROJECT_SOURCE_DIR}/third_party/fmt/include
        ${PROJECT_SOURCE_DIR}/third_party/libpg_query/include
        ${PROJECT_SOURCE_DIR}/third_party/argparse/include
        ${PROJECT_SOURCE_DIR}/third_party/cpp_random_distributions
        ${PROJECT_SOURCE_DIR}/third_party/backward-cpp/include
)

include_directories(${BUSTUB_SRC_INCLUDE_DIR} ${BUSTUB_TEST_INCLUDE_DIR} ${BUSTUB_THIRD_PARTY_INCLUDE_DIR})
include_directories(BEFORE src) # This is needed for gtest. ???

function(disable_target_warnings NAME)
        target_compile_options(${NAME} PRIVATE "-w")
endfunction()
```

#### 3. 依赖第三方库

```cmake
find_package(LibElf)
find_package(LibDwarf)
```

#### 4. 集成 module

```cmake
add_subdirectory(third_party)
add_subdirectory(src)
add_subdirectory(test)
add_subdirectory(tools)
```

#### 5. make targets

通过 add_custom_target

```cmake
add_custom_target(check-clang-tidy-p4
        ${BUSTUB_BUILD_SUPPORT_DIR}/run_clang_tidy.py # run LLVM's clang-tidy script
        -clang-tidy-binary ${CLANG_TIDY_BIN} # using our clang-tidy binary
        -p ${CMAKE_BINARY_DIR} # using cmake's generated compile commands
        ${P4_FILES}
)

add_custom_target(submit-p4
        zip project4-submission.zip
        ${P4_FILES}
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)
```

## test - CMakeLists.txt

为 test 目录下的每个测试 cpp 文件创建可执行文件, 使用第三方库 gtest、gmock_main

```cmake
# #########################################
# "make XYZ_test"
# #########################################
foreach (bustub_test_source ${BUSTUB_TEST_SOURCES})
    # Create a human readable name.
    get_filename_component(bustub_test_filename ${bustub_test_source} NAME) # xxx.cpp
    string(REPLACE ".cpp" "" bustub_test_name ${bustub_test_filename}) # xxx

    # Add the test target separately and as part of "make check-tests".
    add_executable(${bustub_test_name} EXCLUDE_FROM_ALL ${bustub_test_source} "${PROJECT_SOURCE_DIR}/tools/backtrace.cpp")
    add_backward(${bustub_test_name})
    add_dependencies(build-tests ${bustub_test_name})
    add_dependencies(check-tests ${bustub_test_name})

    gtest_discover_tests(${bustub_test_name}
            EXTRA_ARGS
            --gtest_output=xml:${CMAKE_BINARY_DIR}/test/${bustub_test_name}.xml
            --gtest_catch_exceptions=0
            DISCOVERY_TIMEOUT 120
            PROPERTIES
            TIMEOUT 120
            )

    target_link_libraries(${bustub_test_name} bustub gtest gmock_main)

    # Set test target properties and dependencies.
    set_target_properties(${bustub_test_name}
            PROPERTIES
            RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/test"
            COMMAND ${bustub_test_name}
            )
endforeach ()
```

## src - CMakeLists.txt

1. 将每个目录集成进来，每个目录下再单独配置 CMakeLists.txt
2. 将所有目录以静态库 bustub 的形式输出
3. 为 bustub 库添加引用的头文件和链接的链接库

以 storage 目录为例

```bash
.
├── CMakeLists.txt
├── disk
│   ├── CMakeLists.txt
│   ├── disk_manager.cpp
│   ├── disk_manager_memory.cpp
│   └── disk_scheduler.cpp
├── index
│   ├── b_plus_tree.cpp
│   ├── b_plus_tree_index.cpp
│   ├── CMakeLists.txt
│   ├── extendible_hash_table_index.cpp
│   ├── index_iterator.cpp
│   └── linear_probe_hash_table_index.cpp
├── page
│   ├── b_plus_tree_internal_page.cpp
│   ├── b_plus_tree_leaf_page.cpp
│   ├── b_plus_tree_page.cpp
│   ├── CMakeLists.txt
│   ├── extendible_htable_bucket_page.cpp
│   ├── extendible_htable_directory_page.cpp
│   ├── extendible_htable_header_page.cpp
│   ├── extendible_htable_page_utils.cpp
│   ├── hash_table_block_page.cpp
│   ├── hash_table_bucket_page.cpp
│   ├── hash_table_directory_page.cpp
│   ├── hash_table_header_page.cpp
│   ├── page_guard.cpp
│   └── table_page.cpp
└── table
    ├── CMakeLists.txt
    ├── table_heap.cpp
    ├── table_iterator.cpp
    └── tuple.cpp
```

根目录 cmake 配置文件

```cmake
add_subdirectory(disk)
add_subdirectory(index)
add_subdirectory(page)
add_subdirectory(table)
```

disk 子目录下 cmake 配置文件

```cmake
add_library(
    bustub_storage_disk
    OBJECT
    disk_manager.cpp
    disk_manager_memory.cpp
    disk_scheduler.cpp)

set(ALL_OBJECT_FILES
    ${ALL_OBJECT_FILES} $<TARGET_OBJECTS:bustub_storage_disk>
    PARENT_SCOPE) # ALL_OBJECT_FILES++
```

1. OBJECT：用于创建对象库，可将给定 add_library 的列表中的源码编译到目标文件，不将它们归档到静态库中， 也不能将它们链接到共享对象中。如果需要一次性创建静态库和动态库，那么使用对象库尤其有用。
2. 使用$<TARGET_OBJECTS:name>引用obj库
3. 如果使用了PARENT_SCOPE选项，意味着该变量的作用域会传递到上一层（也就是上一层目录或者当前函数的调用者，如果是函数则传递到函数的调用者，如果是目录则传递到上一层目录）
4. 将所有的obj库都存入ALL_OBJECT_FILES变量中

## tools - CMakeLists.txt

tools 目录下的每个子目录都是一个可执行文件

## third_party - CMakeLists.txt

将每个目录作为 third_party 的子 module 集成到 project 中

```cmake
add_subdirectory(googletest)
add_subdirectory(fmt)
```

## CMU15445使用CMake的经验应用到其他一般项目的启示


## 解决不了的问题

PATH_TO_CMAKELISTS_TXT 是什么含义
file(TO_CMAKE_PATH "${PROJECT_BINARY_DIR}/CMakeLists.txt" PATH_TO_CMAKELISTS_TXT)是什么含义
include_directories(BEFORE src) # This is needed for gtest.
